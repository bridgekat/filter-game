import FilterGame.Basic

set_option linter.unusedVariables false
set_option autoImplicit false

namespace FilterGame
variable {α : Type _}

/-!
# Filter basis

We define the filter basis in this file.

# Main Definitions

`filter_basis` : A filter basis is a nonempty collection of sets such that the
intersection of two elements of this collection contains
some element of the collection.
-/

-- Let's define the filter basis:
/-- A filter basis is a nonempty collection of sets such that the intersection of
two elements of this collection contains some element of the collection. -/
structure Basis (α : Type _) where
  sets                 : Set (Set α)
  sets_nonempty        : ∃ s, s ∈ sets
  inter_mem_sets {s t} : s ∈ sets → t ∈ sets → ∃ u ∈ sets, u ⊆ s ∩ t

-- (Technical detail)
-- A filter basis B is a collection of subsets,
-- so clearly we want to do something like U ∈ B.
instance : Membership (Set α) (Basis α) :=
  ⟨fun s b ↦ s ∈ b.sets⟩

-- (Technical detail)
-- Make propositional equality from definition, so it can be tagged with `simp`.
@[simp]
theorem Basis.mem_def (b : Basis α) (s : Set α) : s ∈ b ↔ s ∈ b.sets := by
  exact Iff.rfl

-- (Technical detail)
-- By proof irrelevance, two filter bases are equal if and only if they contain the same sets.
@[simp]
theorem Basis.eq_def (b c : Basis α) : b = c ↔ b.sets = c.sets := by
  apply Iff.intro
  . intro h; rw [h]
  . intro h; cases b; cases c; congr

theorem Basis.ext_iff (b c : Basis α) : b = c ↔ (∀ s, s ∈ b ↔ s ∈ c) := by
  simp_rw [eq_def, mem_def, Set.ext_iff]

theorem Basis.ext {b c : Basis α} : (∀ s, s ∈ b ↔ s ∈ c) → b = c := by
  exact (ext_iff _ _).mpr

-- We can prove that every filter is actually a filter basis.
/-- View a filter as a filter basis. -/
def Filter.as_basis (f : Filter α) : Basis α :=
{ sets := f.sets
  sets_nonempty := by
    exact ⟨Set.univ, univ_mem _⟩,
  inter_mem_sets := by
    intro s t hs ht
    exact ⟨s ∩ t, inter_mem hs ht, subset_rfl⟩ }

-- Here are some APIs of filter basis:
theorem Filter.mem_as_basis_def (f : Filter α) (t : Set α) : t ∈ Filter.as_basis f ↔ t ∈ f := by
  exact Iff.rfl

/- Actually, we can construct filter using filter basis:
The filter f generated by filter base B is te collection of all sets fⱼ such
that Bᵢ ⊆ fⱼ for some Bᵢ ∈ B. -/

-- TODO: not required but helpful
theorem Set.mem_iff (p : α → Prop) (y : α) : y ∈ {x | p x} ↔ p y := by
  exact Set.mem_setOf

-- Try to solve these `sorry`s.
/-- The filter associated to a filter basis. -/
def Basis.as_filter (b : Basis α) : Filter α :=
{ sets := {s | ∃ t ∈ b, t ⊆ s}
  univ_mem_sets := by
    rw [Set.mem_iff]
    have ⟨s, hs⟩ := sets_nonempty b
    refine ⟨s, hs, Set.subset_univ _⟩
  superset_mem_sets := by
    intros s t hs h
    rw [Set.mem_iff] at hs ⊢
    have ⟨u, hu⟩ := hs
    exact ⟨u, hu.1, subset_trans hu.2 h⟩
  inter_mem_sets := by
    intros s t hs ht
    rw [Set.mem_iff] at *
    have ⟨u, hu⟩ := hs
    have ⟨v, hv⟩ := ht
    have ⟨w, hw⟩ := Basis.inter_mem_sets b hu.1 hv.1
    have ⟨hwu, hwv⟩ := Set.subset_inter_iff.mp hw.2
    refine ⟨w, hw.1, Set.subset_inter ?_ ?_⟩
    . exact subset_trans hwu hu.2
    . exact subset_trans hwv hv.2 }

-- The following two lemmas are directly from the definition.
theorem Basis.mem_as_filter_def (b : Basis α) (s : Set α) : s ∈ b.as_filter ↔ ∃ t ∈ b, t ⊆ s := by
  exact Iff.rfl

theorem Basis.mem_as_filter_of_mem {b : Basis α} {s : Set α} (h : s ∈ b) : s ∈ b.as_filter := by
  rw [Basis.mem_as_filter_def]
  exact ⟨s, h, subset_rfl⟩

-- Next, we can try to prove this lemma:

/-- The filter generated by a filter is actually itself. -/
theorem Filter.as_basis_as_filter_eq_self (f : Filter α) : f.as_basis.as_filter = f := by
  rw [eq_def]
  apply Set.ext; intros x
  simp_rw [← mem_def, Basis.mem_as_filter_def, mem_as_basis_def]
  apply Iff.intro
  . intros hx; have ⟨t, ht⟩ := hx; exact superset_mem ht.1 ht.2
  . intros hx; exact ⟨x, hx, subset_rfl⟩

end FilterGame
