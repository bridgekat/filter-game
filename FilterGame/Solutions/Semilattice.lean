import FilterGame.Solutions.Principal

set_option linter.unusedVariables false
set_option autoImplicit false

namespace FilterGame
variable {Î± : Type _}

/-!
# Semilattice

This level contains some challenging puzzles which ultimately lead to the proof
that the `â‰¤` defined on filters, along with a notion of "greatest lower bounds",
make an algebraic structure called "semilattice".

Hint: when confronted by a goal like `âˆƒ s, ...`, you have to come up with a
choice of `s` by yourself. Decide which `s` to use by drawing Venn diagrams.
-/

/--
The "greatest lower bound", or "infimum", of two filters `f` and `g` is defined
as the filter consisting of intersections of elements from `f` and `g`.
-/
instance : Min (Filter Î±) := âŸ¨fun f g â†¦
{ sets := { s | âˆƒ a âˆˆ f, âˆƒ b âˆˆ g, s = a âˆ© b }
  univ_mem_sets := by
    rw [Set.mem_iff]
    refine âŸ¨Set.univ, f.univ_mem, Set.univ, g.univ_mem, ?_âŸ©
    rw [Set.inter_self]
  superset_mem_sets := by
    intros s t hs hst
    rw [Set.mem_iff] at hs âŠ¢
    have âŸ¨a, ha, b, hb, hsabâŸ© := hs
    refine âŸ¨a âˆª t, f.superset_mem ha ?_, b âˆª t, g.superset_mem hb ?_, ?_âŸ©
    Â· exact Set.subset_union_left
    Â· exact Set.subset_union_left
    rw [â† Set.inter_union_distrib_right, â† hsab, Set.union_eq_self_of_subset_left hst]
  inter_mem_sets := by
    intros s t hs ht
    rw [Set.mem_iff] at *
    have âŸ¨sa, hsa, sb, hsb, hsabâŸ© := hs
    have âŸ¨ta, hta, tb, htb, htabâŸ© := ht
    refine âŸ¨sa âˆ© ta, f.inter_mem hsa hta, sb âˆ© tb, g.inter_mem hsb htb, ?_âŸ©
    rw [hsab, htab]
    ac_rfl }âŸ©

/-!
The infimum of `f` and `g` is written as `f âŠ“ g`, where `âŠ“` can be typed into
Lean using `\inf`.
-/

theorem Filter.mem_inf_def {f g : Filter Î±} {s : Set Î±} :
    s âˆˆ f âŠ“ g â†” âˆƒ a âˆˆ f, âˆƒ b âˆˆ g, s = a âˆ© b := by
  exact Iff.rfl

theorem Filter.inter_mem_inf {f g : Filter Î±} {s t : Set Î±} (hs : s âˆˆ f) (ht : t âˆˆ g) :
    s âˆ© t âˆˆ f âŠ“ g := by
  exact âŸ¨s, hs, t, ht, rflâŸ©

--! Treasure "semilattice" unlocked!
instance : SemilatticeInf (Filter Î±) :=
{ inf := instMinFilter.min
  inf_le_left := by
    intros f g s hs
    rw [Filter.mem_inf_def]
    refine âŸ¨s, hs, Set.univ, Filter.univ_mem _, ?_âŸ©
    rw [Set.inter_univ]
  inf_le_right := by
    intros a b s hs
    rw [Filter.mem_inf_def]
    refine âŸ¨Set.univ, Filter.univ_mem _, s, hs, ?_âŸ©
    rw [Set.univ_inter]
  le_inf := by
    intros f g k hfg hfk s hs
    have âŸ¨a, ha, b, hb, hsabâŸ© := hs
    specialize hfg _ ha
    specialize hfk _ hb
    rw [hsab]
    exact Filter.inter_mem hfg hfk }

/-!
However, on our way to the ultrafilter world still resides a hidden boss...

Hint for the forward direction: use `Filter.mem_inf_def`.
Hint for the backward direction: consider `s` as `(t âˆª s) âˆ© (tá¶œ âˆª s)`.
-/

theorem Filter.mem_inf_principal_iff {f : Filter Î±} {s t : Set Î±} :
    s âˆˆ f âŠ“ ğ“Ÿ t â†” {x | x âˆˆ t â†’ x âˆˆ s} âˆˆ f := by
  apply Iff.intro
  Â· intros hs
    rw [mem_inf_def] at hs
    have âŸ¨a, ha, b, hb, hsabâŸ© := hs
    clear hs
    rw [hsab]; clear hsab
    rw [mem_principal_def] at hb
    suffices h : a âŠ† {x | x âˆˆ t â†’ x âˆˆ a âˆ© b} by
      exact superset_mem ha h
    intros x hxa hxt
    exact âŸ¨hxa, hb hxtâŸ©
  Â· intros h
    have heq : {x | x âˆˆ t â†’ x âˆˆ s} = tá¶œ âˆª s := by
      apply Set.ext; intros x
      rw [Set.mem_iff, Set.mem_union, Set.mem_compl_iff, imp_iff_not_or]
    rw [heq] at h; clear heq
    have heq : s = (tá¶œ âˆª s) âˆ© (t âˆª s) := by
      rw [â† Set.inter_union_distrib_right, Set.compl_inter_self, Set.empty_union]
    rw [heq]; clear heq
    refine inter_mem_inf h ?_
    rw [mem_principal_def]
    exact Set.subset_union_left

end FilterGame
